apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    
    defaultConfig {
        applicationId "app.abhijit.iter"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 25
        versionName "v2.6.2"
        resConfig "en"
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version", variant.versionName

        if (variant.buildType.name == "debug") {
            variant.resValue "string", "app_version", "dev build " + new Date().format('yyyyMMddHHmmss')
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-square-okhttp3.pro'
            proguardFiles 'proguard-gson.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    lintOptions {
        lintConfig file("lint.xml")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.7.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.4'
}

repositories {
    maven {
        url 'https://maven.google.com'
    }
}
